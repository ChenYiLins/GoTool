.版本 2
.支持库 EThread
.支持库 shell
.支持库 eAPI

.程序集 程序入口
.程序集变量 集_窗口_启动窗口, Ex_DirectUI
.程序集变量 集_窗口_遮罩窗口, Ex_DirectUI

.程序集变量 集_句柄_窗口, 整数型
.程序集变量 集_句柄_DLL, 整数型
.程序集变量 集_句柄_线程, 整数型

.程序集变量 集_启动窗口_导航, 侧边导航_Ex

.程序集变量 集_启动窗口_页面, 页面_Ex, , "5"
.程序集变量 集_启动窗口_页面_句柄, 整数型, , "5"

.程序集变量 集_启动窗口_页面1_按钮, 按钮_Ex

.程序集变量 集_启动窗口_页面2_列表, 列表视图_Ex
.程序集变量 集_启动窗口_页面2_标签, 标签_Ex

.程序集变量 集_启动窗口_页面3_列表, 列表视图_Ex
.程序集变量 集_启动窗口_页面3_标签, 标签_Ex

.程序集变量 集_启动窗口_页面4_图片框, 图片框_Ex, , "2"
.程序集变量 集_启动窗口_页面4_标签, 标签_Ex, , "10"
.程序集变量 集_启动窗口_页面4_链接, 链接框_Ex

.程序集变量 集_启动窗口_页面5_标签, 标签_Ex, , "5"
.程序集变量 集_启动窗口_页面5_编辑框, 编辑框_Ex
.程序集变量 集_启动窗口_页面5_按钮, 按钮_Ex, , "2"
.程序集变量 集_启动窗口_页面5_选择框, 选择框_Ex, , "3"


.子程序 _启动子程序, 整数型, , 本子程序在程序启动后最先执行
    .局部变量 局_循环计数, 整数型

    ' ******************************** ********************************
    ' EXDUI窗口创建
    集_句柄_DLL ＝ _LoadLibrary (取指针_字节集型 (#Dll_ExduiDll), 0, 0)
    全局_初始化 (集_句柄_DLL, 真, #资源_主题包, , { }, “微软雅黑”, 12, #字体风格_普通, #文本渲染模式_系统默认, , , )
    集_句柄_窗口 ＝ 窗口_创建 (, , , , 1080, 680, 假)
    集_窗口_启动窗口.事件_背景重画 (&窗口_重画, )
    集_窗口_启动窗口.挂接窗口 (, 集_句柄_窗口, #资源_主题包, , , #白色, #标题栏_最小化按钮 ＋ #标题栏_关闭按钮 ＋ #标题栏_标题, , #底图方式_拉伸, , 假, , , )
    集_窗口_启动窗口.置客户区偏移 (0, 0, 0, 0, 真)
    ' ********************************
    ' 初始化窗口外观属性
    窗口_置圆角 (集_句柄_窗口, #窗口样式_圆角)
    ' 窗口_置亚克力模糊 (集_句柄_窗口, #模糊样式_亚克力, ) ‘测试代码
    ' ******************************** ********************************
    ' 主界面
    集_启动窗口_页面_句柄 [1] ＝ 集_启动窗口_页面 [1].创建 (集_句柄_窗口, 48, 32, 1032, 648, 真)
    集_启动窗口_页面1_按钮.创建 (集_启动窗口_页面_句柄 [1], 800, 580, 200, 55, “启动游戏”, RGB2ARGB (#白色), “微软雅黑”, 20)
    ' ******************************** ********************************
    ' 脚本
    集_启动窗口_页面_句柄 [2] ＝ 集_启动窗口_页面 [2].创建 (集_句柄_窗口, 48, 32, 1032, 648, 假)
    集_启动窗口_页面2_标签.创建 (集_启动窗口_页面_句柄 [2], 48, 29, 210, 59, “脚本”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 26)
    集_启动窗口_页面2_列表.创建 (集_启动窗口_页面_句柄 [2], 27, 88, 970, 520, #列表类型_列表, 真, , 真, 真, , , RGB2ARGB (#白色), RGB2ARGB (取颜色值 (39, 39, 39)), , 14, , RGB2ARGB (取颜色值 (39, 39, 39)), 真)
    ' **************
    集_启动窗口_页面2_列表.动画帧数 (8, 8)
    ' **************
    集_启动窗口_页面2_列表.插入列 (1, , 965)
    集_启动窗口_页面2_列表.事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面2_列表.事件_检查框状态被改变 (&事件_列表框事件)
    ' 计次循环首 (5, )     *测试代码
    ' 集_启动窗口_页面2_列表.插入表项 (, “我是傻逼”, , , , , , 真) ’测试代码
    ' **************
    ' 载入脚本列表
    文件_枚举文件夹 (取运行目录 () ＋ “\Script”, 全_列表_脚本, 假, 假)
    ' 调试输出 (全_列表_脚本)  ' 测试代码
    .计次循环首 (取数组成员数 (全_列表_脚本), 局_循环计数)
        集_启动窗口_页面2_列表.插入表项 (, 全_列表_脚本 [局_循环计数], , , , , , 真)
        .如果真 (读配置项 (取运行目录 () ＋ “\Script\” ＋ 全_列表_脚本 [局_循环计数] ＋ “\Config.ini”, “Config”, “Function”, ) ＝ “true”)
            集_启动窗口_页面2_列表.置状态图片 (局_循环计数, 1)
        .如果真结束
        
    .计次循环尾 ()
    ' ******************************** ********************************
    ' 启动项
    集_启动窗口_页面_句柄 [3] ＝ 集_启动窗口_页面 [3].创建 (集_句柄_窗口, 48, 32, 1032, 648, 假)
    集_启动窗口_页面3_标签.创建 (集_启动窗口_页面_句柄 [3], 48, 29, 210, 59, “启动项”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 26)
    集_启动窗口_页面3_列表.创建 (集_启动窗口_页面_句柄 [3], 27, 88, 970, 520, #列表类型_列表, 真, , 真, 真, , , RGB2ARGB (#白色), RGB2ARGB (取颜色值 (39, 39, 39)), , 14, , RGB2ARGB (取颜色值 (39, 39, 39)), 真)
    ' **************
    集_启动窗口_页面3_列表.动画帧数 (8, 8)
    ' **************
    集_启动窗口_页面3_列表.插入列 (1, , 965)
    集_启动窗口_页面3_列表.事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面3_列表.事件_检查框状态被改变 (&事件_列表框事件)
    ' 计次循环首 (5, )     *测试代码
    ' 集_启动窗口_页面3_列表.插入表项 (, “我是傻逼”, , , , , , 真)  ’测试代码
    ' **************
    ' 载入启动项列表
    文件_枚举文件夹 (取运行目录 () ＋ “\Mscfg”, 全_列表_启动项, 假, 假)
    ' 调试输出 (全_列表_启动项)  ' 测试代码
    .计次循环首 (取数组成员数 (全_列表_启动项), 局_循环计数)
        集_启动窗口_页面3_列表.插入表项 (, 全_列表_启动项 [局_循环计数], , , , , , 真)
        .如果真 (读配置项 (取运行目录 () ＋ “\Mscfg\” ＋ 全_列表_启动项 [局_循环计数] ＋ “\Config.ini”, “Config”, “Function”, ) ＝ “true”)
            集_启动窗口_页面3_列表.置状态图片 (局_循环计数, 1)
        .如果真结束
        
    .计次循环尾 ()
    ' 载入启动项
    全_文本_启动项 ＝ “”
    .计次循环首 (集_启动窗口_页面3_列表.取表项数 (), 局_循环计数)
        .如果真 (集_启动窗口_页面3_列表.取当前状态图片 (局_循环计数) ＝ 1)
            全_文本_启动项 ＝ 全_文本_启动项 ＋ 到文本 (读入文件 (取运行目录 () ＋ “\Mscfg\” ＋ 集_启动窗口_页面3_列表.取标题 (局_循环计数) ＋ “\Mscfg.txt”)) ＋ “ ”
        .如果真结束
        
    .计次循环尾 ()
    ' 调试输出 (全_文本_启动项)  ' 测试代码
    ' ******************************** ********************************
    ' 关于
    集_启动窗口_页面_句柄 [4] ＝ 集_启动窗口_页面 [4].创建 (集_句柄_窗口, 48, 32, 1032, 648, 假)
    集_启动窗口_页面4_图片框 [1].创建 (集_启动窗口_页面_句柄 [4], 42, 88, 115, 115, #图片_LOGO)
    集_启动窗口_页面4_标签 [1].创建 (集_启动窗口_页面_句柄 [4], 48, 29, 210, 59, “关于”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 26)
    集_启动窗口_页面4_标签 [2].创建 (集_启动窗口_页面_句柄 [4], 192, 106, 210, 59, “GoTool”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 24, #字体风格_加粗)
    集_启动窗口_页面4_标签 [3].创建 (集_启动窗口_页面_句柄 [4], 192, 146, 210, 59, “@ChenYiLin”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_标签 [4].创建 (集_启动窗口_页面_句柄 [4], 52, 205, 210, 59, “Version”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 24)
    集_启动窗口_页面4_标签 [5].创建 (集_启动窗口_页面_句柄 [4], 52, 263, 210, 59, “Programer:v2.5”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_标签 [6].创建 (集_启动窗口_页面_句柄 [4], 52, 304, 210, 59, “Update:19”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_标签 [7].创建 (集_启动窗口_页面_句柄 [4], 52, 345, 268, 59, “ExDirectUI:3.0.15.510”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_标签 [8].创建 (集_启动窗口_页面_句柄 [4], 52, 386, 268, 59, “WinUI Lib:1.3”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_标签 [9].创建 (集_启动窗口_页面_句柄 [4], 52, 427, 268, 59, “DocumentSearch:1.3”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    集_启动窗口_页面4_图片框 [2].创建 (集_启动窗口_页面_句柄 [4], 519, 265, 42, 42, #图片_Gitee)
    集_启动窗口_页面4_标签 [10].创建 (集_启动窗口_页面_句柄 [4], 530, 205, 210, 59, “Follow us”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 24)
    集_启动窗口_页面4_链接.创建 (集_启动窗口_页面_句柄 [4], 571, 263, 210, 59, “Gitee”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 20)
    ' **************
    集_启动窗口_页面4_链接.访问后颜色 (RGB2ARGB (取颜色值 (234, 84, 48)))
    集_启动窗口_页面4_链接.地址 (“https://gitee.com/ChenYiLinS/GoTool”)
    ' ******************************** ********************************
    ' 设置
    集_启动窗口_页面_句柄 [5] ＝ 集_启动窗口_页面 [5].创建 (集_句柄_窗口, 48, 32, 1032, 648, 假)
    集_启动窗口_页面5_标签 [1].创建 (集_启动窗口_页面_句柄 [5], 48, 29, 210, 59, “设置”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 26)
    集_启动窗口_页面5_标签 [2].创建 (集_启动窗口_页面_句柄 [5], 48, 73, 210, 59, “CSGO路径”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 18, , )
    集_启动窗口_页面5_标签 [3].创建 (集_启动窗口_页面_句柄 [5], 87, 127, 210, 59, “开机自动启动”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 15)
    集_启动窗口_页面5_标签 [4].创建 (集_启动窗口_页面_句柄 [5], 87, 167, 210, 59, “隐藏到托盘”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 15)
    集_启动窗口_页面5_标签 [5].创建 (集_启动窗口_页面_句柄 [5], 87, 207, 210, 59, “窗口模糊效果”, RGB2ARGB (#白色), , , , , , “微软雅黑”, 15)
    集_启动窗口_页面5_编辑框.创建 (集_启动窗口_页面_句柄 [5], 167, 86, 435, 32, , RGB2ARGB (#白色), , “微软雅黑”, 13)
    集_启动窗口_页面5_按钮 [1].创建 (集_启动窗口_页面_句柄 [5], 622, 86, 53, 31, “...”, RGB2ARGB (#白色), “微软雅黑”, 13)
    集_启动窗口_页面5_按钮 [2].创建 (集_启动窗口_页面_句柄 [5], 699, 86, 100, 31, “自动查找”, RGB2ARGB (#白色), “微软雅黑”, 14)
    集_启动窗口_页面5_选择框 [1].创建 (集_启动窗口_页面_句柄 [5], 48, 147, 20, 20)
    集_启动窗口_页面5_选择框 [2].创建 (集_启动窗口_页面_句柄 [5], 48, 187, 20, 20)
    集_启动窗口_页面5_选择框 [3].创建 (集_启动窗口_页面_句柄 [5], 48, 227, 20, 20)
    ' **************
    集_启动窗口_页面5_编辑框.事件_字符输入 (&事件_回调事件)
    集_启动窗口_页面5_按钮 [1].事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面5_按钮 [2].事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面5_选择框 [1].事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面5_选择框 [2].事件_鼠标左键被放开 (&事件_回调事件)
    集_启动窗口_页面5_选择框 [3].事件_鼠标左键被放开 (&事件_回调事件)
    ' **************
    ' 设置功能从“Config.ini”配置文件载入
    集_启动窗口_页面5_编辑框.内容 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “Path”, ))
    .判断开始 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “StartWithWindows”, ) ＝ “false”)
        集_启动窗口_页面5_选择框 [1].选中 (假)
    .默认
        集_启动窗口_页面5_选择框 [1].选中 (真)
    .判断结束
    .判断开始 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “MiniIcon”, ) ＝ “false”)
        集_启动窗口_页面5_选择框 [2].选中 (假)
    .默认
        集_启动窗口_页面5_选择框 [2].选中 (真)
    .判断结束
    .判断开始 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “BlurWindow”, ) ＝ “false”)
        集_启动窗口_页面5_选择框 [3].选中 (假)
        窗口_置亚克力模糊 (集_句柄_窗口, #模糊样式_默认, )
    .默认
        集_启动窗口_页面5_选择框 [3].选中 (真)
        窗口_置亚克力模糊 (集_句柄_窗口, #模糊样式_亚克力, )
    .判断结束
    ' ******************************** ********************************
    ' 导航栏创建
    集_启动窗口_导航.创建 (集_句柄_窗口, 680, 4, 真, 取颜色值 (234, 84, 48))
    集_启动窗口_导航.加入项目 (1, “主界面”, , “”)
    集_启动窗口_导航.加入项目 (2, “脚本”, , “憽)
    集_启动窗口_导航.加入项目 (3, “启动项”, , “”)
    集_启动窗口_导航.加入项目 (4, “关于”, , “”)
    集_启动窗口_导航.事件_导航栏被单击 (&事件_导航栏)
    ' ******************************** ********************************
    集_窗口_启动窗口.置进入特效 (#特效类型_窗口_渐隐渐显, , , , #缓动类型_三次方缓动_In)
    集_窗口_启动窗口.置退出特效 (#特效类型_窗口_弹性伸缩, , , , #缓动类型_三次方缓动_Out)
    窗口_消息循环 (集_句柄_窗口)
    _FreeLibrary (集_句柄_DLL)
    返回 (0) ' 可以根据您的需要返回任意数值

.子程序 事件_回调事件
    .参数 hObj, 整数型

    .判断开始 (hObj ＝ 集_启动窗口_页面1_按钮.取组件句柄 ())
        运行 (文件_寻找_进程 (“steam.exe”) ＋ “ -applaunch 730 ” ＋ 全_文本_启动项, 假, )
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面2_列表.取组件句柄 ())
        ' ******************************** ********************************
        ' 调试输出 (全_列表_脚本 [集_启动窗口_页面2_列表.现行选中项 ()]) ' 测试代码
        .如果真 (集_启动窗口_页面2_列表.现行选中项 () ＞ 0)
            .如果 (集_启动窗口_页面5_选择框 [3].选中 () ＝ 真) ' 是否启用模糊效果
                .如果真 (运行 (取运行目录 () ＋ “\Script\” ＋ 全_列表_脚本 [集_启动窗口_页面2_列表.现行选中项 ()] ＋ “\Console.exe ” ＋ 到文本 (集_句柄_窗口) ＋ “ ” ＋ 到文本 (集_窗口_遮罩窗口.取窗口句柄 ()), 假, ))
                    .如果真 (集_窗口_遮罩窗口.取窗口句柄 () ＝ 0) ' 防止创建多余的遮罩窗口
                        启动线程 (&遮罩窗口_创建, , )
                    .如果真结束
                    .判断循环首 (集_窗口_遮罩窗口.取窗口句柄 () ＝ 0) ' 防止窗口未创建
                    .判断循环尾 ()
                    ' 调试输出 (集_窗口_遮罩窗口.取窗口句柄 ()) ' 测试代码
                .如果真结束
                集_启动窗口_页面2_列表.现行选中项 (0)
                ' **************
            .否则
                运行 (取运行目录 () ＋ “\Script\” ＋ 全_列表_脚本 [集_启动窗口_页面2_列表.现行选中项 ()] ＋ “\Console.exe ” ＋ 到文本 (集_句柄_窗口), 假, )
                集_启动窗口_页面2_列表.现行选中项 (0)
            .如果结束
            ' ******************************** ********************************
        .如果真结束
        
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面3_列表.取组件句柄 ())
        .如果真 (集_启动窗口_页面3_列表.现行选中项 () ＞ 0)
            .如果 (集_启动窗口_页面5_选择框 [3].选中 () ＝ 真) ' 是否启用模糊效果
                .如果真 (运行 (取运行目录 () ＋ “\Mscfg\” ＋ 全_列表_启动项 [集_启动窗口_页面3_列表.现行选中项 ()] ＋ “\Console.exe ” ＋ 到文本 (集_句柄_窗口) ＋ “ ” ＋ 到文本 (集_窗口_遮罩窗口.取窗口句柄 ()), 假, ))
                    .如果真 (集_窗口_遮罩窗口.取窗口句柄 () ＝ 0) ' 防止创建多余的遮罩窗口
                        启动线程 (&遮罩窗口_创建, , )
                    .如果真结束
                    .判断循环首 (集_窗口_遮罩窗口.取窗口句柄 () ＝ 0) ' 防止窗口未创建
                    .判断循环尾 ()
                .如果真结束
                集_启动窗口_页面3_列表.现行选中项 (0)
                ' **************
            .否则
                运行 (取运行目录 () ＋ “\Mscfg\” ＋ 全_列表_启动项 [集_启动窗口_页面3_列表.现行选中项 ()] ＋ “\Console.exe ” ＋ 到文本 (集_句柄_窗口), 假, )
                集_启动窗口_页面3_列表.现行选中项 (0)
            .如果结束
            ' ******************************** ********************************
        .如果真结束
        
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_编辑框.取组件句柄 ())
        写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “Path”, 集_启动窗口_页面5_编辑框.内容 ())
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_按钮 [1].取组件句柄 ())
        集_启动窗口_页面5_编辑框.内容 (浏览文件夹 (“选择CSGO目录”, ))
        .如果真 (集_启动窗口_页面5_编辑框.内容 () ≠ “”)
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “Path”, 集_启动窗口_页面5_编辑框.内容 ())
        .如果真结束
        
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_按钮 [2].取组件句柄 ())
        .如果 (集_启动窗口_页面5_按钮 [2].标题 () ＝ “自动查找”)
            集_启动窗口_页面5_按钮 [2].标题 (“停止查找”)
            ' ********************************
            .如果 (文件_寻找_进程 (“csgo.exe”) ≠ “”)
                ' ********************************
                ' 通过进程查找到CSGO路径
                集_启动窗口_页面5_按钮 [2].标题 (“自动查找”)
                集_启动窗口_页面5_编辑框.内容 (文件_寻找_进程 (“csgo.exe”))
            .否则
                ' ********************************
                ' 进程查找失败，转为硬盘查找
                启动线程 (&事件_寻找文件, , 集_句柄_线程)
                ' ********************************
            .如果结束
            
        .否则
            集_启动窗口_页面5_按钮 [2].标题 (“自动查找”)
            强制结束线程 (集_句柄_线程)
            文件_寻找_停止 ()
        .如果结束
        
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_选择框 [1].取组件句柄 ())
        ' ******************************** ********************************
        .如果 (集_启动窗口_页面5_选择框 [1].选中 () ＝ 真)
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “StartWithWindows”, “false”)
            取消自动运行 (“GoTool”)
        .否则
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “StartWithWindows”, “true”)
            设置自动运行 (“GoTool”, 取运行目录 () ＋ “\GoTool.exe”, #接口常量.到注册表)
        .如果结束
        ' ******************************** ********************************
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_选择框 [2].取组件句柄 ())
        ' ******************************** ********************************
        .如果 (集_启动窗口_页面5_选择框 [2].选中 () ＝ 真)
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “MiniIcon”, “false”)
        .否则
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “MiniIcon”, “true”)
        .如果结束
        ' ******************************** ********************************
    .默认
        
    .判断结束
    
    .判断开始 (hObj ＝ 集_启动窗口_页面5_选择框 [3].取组件句柄 ())
        ' ******************************** ********************************
        .如果 (集_启动窗口_页面5_选择框 [3].选中 () ＝ 真)
            ' 调试输出 (“我是傻逼1号”) ' 测试代码
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “BlurWindow”, “false”)
            窗口_置亚克力模糊 (集_句柄_窗口, #模糊样式_默认)
        .否则
            ' 调试输出 (“我是傻逼2号”) ' 测试代码
            写配置项 (取运行目录 () ＋ “\Config.ini”, “Config”, “BlurWindow”, “true”)
            窗口_置亚克力模糊 (集_句柄_窗口, #模糊样式_亚克力)
        .如果结束
        ' ******************************** ********************************
    .默认
        
    .判断结束
    

.子程序 事件_列表框事件
    .参数 hObj, 整数型
    .参数 表项索引, 整数型
    .参数 检查框状态, 逻辑型
    .局部变量 局_循环计数, 整数型
    .局部变量 局_脚本文件名, 文本型

    .如果 (寻找文件 (集_启动窗口_页面5_编辑框.内容 () ＋ “\csgo.exe”, ) ≠ “”)
        .判断开始 (hObj ＝ 集_启动窗口_页面2_列表.取组件句柄 ())
            局_脚本文件名 ＝ 读配置项 (取运行目录 () ＋ “\Script\” ＋ 集_启动窗口_页面2_列表.取标题 (表项索引) ＋ “\Config.ini”, “Config”, “Cmd”, )
            .如果 (检查框状态 ＝ 真) ' 检查框被选中
                .如果 (寻找文件 (取运行目录 () ＋ “\Script\” ＋ 集_启动窗口_页面2_列表.取标题 (表项索引) ＋ “\” ＋ 局_脚本文件名, ) ≠ “”)
                    复制文件 (取运行目录 () ＋ “\Script\” ＋ 集_启动窗口_页面2_列表.取标题 (表项索引) ＋ “\” ＋ 局_脚本文件名, 集_启动窗口_页面5_编辑框.内容 () ＋ “\csgo\cfg\” ＋ 局_脚本文件名)
                    写配置项 (取运行目录 () ＋ “\Script\” ＋ 集_启动窗口_页面2_列表.取标题 (表项索引) ＋ “\Config.ini”, “Config”, “Function”, “true”)
                .否则
                    信息框 (“请初始化建立脚本”, 0, , 集_句柄_窗口)
                    集_启动窗口_页面2_列表.置状态图片 (表项索引, 0)
                .如果结束
                ' ******************************** ********************************
            .否则
                写配置项 (取运行目录 () ＋ “\Script\” ＋ 集_启动窗口_页面2_列表.取标题 (表项索引) ＋ “\Config.ini”, “Config”, “Function”, “false”)
                删除文件 (集_启动窗口_页面5_编辑框.内容 () ＋ “\csgo\cfg\” ＋ 局_脚本文件名) ' 检查框被取消
            .如果结束
            ' ******************************** ********************************
        .默认
            
        .判断结束
        
        .判断开始 (hObj ＝ 集_启动窗口_页面3_列表.取组件句柄 ())
            .如果 (检查框状态 ＝ 真)
                .如果 (寻找文件 (取运行目录 () ＋ “\Mscfg\” ＋ 集_启动窗口_页面3_列表.取标题 (表项索引) ＋ “\Mscfg.txt”, ) ≠ “”)
                    写配置项 (取运行目录 () ＋ “\Mscfg\” ＋ 集_启动窗口_页面3_列表.取标题 (表项索引) ＋ “\Config.ini”, “Config”, “Function”, “true”)
                .否则
                    信息框 (“请初始化建立启动项”, 0, , 集_句柄_窗口)
                    集_启动窗口_页面3_列表.置状态图片 (表项索引, 0)
                .如果结束
                
            .否则
                写配置项 (取运行目录 () ＋ “\Mscfg\” ＋ 集_启动窗口_页面3_列表.取标题 (表项索引) ＋ “\Config.ini”, “Config”, “Function”, “false”)
            .如果结束
            全_文本_启动项 ＝ “”
            .计次循环首 (集_启动窗口_页面3_列表.取表项数 (), 局_循环计数)
                .如果真 (集_启动窗口_页面3_列表.取当前状态图片 (局_循环计数) ＝ 1)
                    全_文本_启动项 ＝ 全_文本_启动项 ＋ 到文本 (读入文件 (取运行目录 () ＋ “\Mscfg\” ＋ 集_启动窗口_页面3_列表.取标题 (局_循环计数) ＋ “\Mscfg.txt”)) ＋ “ ”
                .如果真结束
                
            .计次循环尾 ()
            ' 调试输出 (全_文本_启动项)  ' 测试代码
        .默认
            
        .判断结束
        
    .否则
        ' ******************************** ********************************
        信息框 (“请选择正确的CSGO目录”, 0, , 集_句柄_窗口)
        集_启动窗口_页面2_列表.置状态图片 (表项索引, 0)
    .如果结束
    

.子程序 事件_导航栏
    .局部变量 局_循环计数, 整数型

    .计次循环首 (5, 局_循环计数)
        .判断开始 (局_循环计数 ＝ 集_启动窗口_导航.现行选中项 ())
            集_启动窗口_页面 [局_循环计数].可视 (真)
        .默认
            集_启动窗口_页面 [局_循环计数].可视 (假)
        .判断结束
        
    .计次循环尾 ()

.子程序 事件_寻找文件
    集_启动窗口_页面5_编辑框.内容 (文件_寻找_硬盘 (“csgo.exe”))
    集_启动窗口_页面5_编辑框.内容 (取文本左边 (集_启动窗口_页面5_编辑框.内容 (), 取文本长度 (集_启动窗口_页面5_编辑框.内容 ()) － 9))
    集_启动窗口_页面5_按钮 [2].标题 (“自动查找”)
    文件_寻找_停止 ()

.子程序 遮罩窗口_创建
    集_窗口_遮罩窗口.事件_背景重画 (&窗口_重画遮罩, )
    集_窗口_遮罩窗口.挂接窗口 (, 窗口_创建 (集_句柄_窗口, , 集_窗口_启动窗口.左边 () ＋ 1, 集_窗口_启动窗口.顶边 () ＋ 1, 1078, 678, 假), #资源_主题包, , , RGB2ARGB (#白色), #标题栏_隐藏, 10, , , , , 16)
    集_窗口_遮罩窗口.可否随意移动 (假)
    ' 调试输出 (集_窗口_遮罩窗口.取窗口句柄 ()) ' 测试代码
    窗口_置圆角 (集_窗口_遮罩窗口.取窗口句柄 (), #窗口样式_圆角)
    窗口_置亚克力模糊 (集_窗口_遮罩窗口.取窗口句柄 (), #模糊样式_亚克力)
    ShowWindow (集_窗口_遮罩窗口.取窗口句柄 (), 0)
    集_窗口_遮罩窗口.置进入特效 (#特效类型_窗口_渐隐渐显, , , , #缓动类型_三次方缓动_In)
    集_窗口_遮罩窗口.置退出特效 (#特效类型_窗口_渐隐渐显, , , , #缓动类型_三次方缓动_Out)
    窗口_消息循环 (集_窗口_遮罩窗口.取窗口句柄 ())
    

