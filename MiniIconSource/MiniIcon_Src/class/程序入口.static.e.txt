.版本 2
.支持库 exui

.程序集 程序入口
.程序集变量 集_句柄_窗口, 整数型
.程序集变量 集_句柄_DLL, 整数型
.程序集变量 集_句柄_图标, 整数型
.程序集变量 集_窗口_启动窗口, Ex_DirectUI
.程序集变量 集_图形按钮, 图形按钮_Ex, , "2"

.子程序 _启动子程序, 整数型, , 本子程序在程序启动后最先执行
    ' ******************************** ********************************
    ' EXDUI窗口创建
    集_句柄_DLL ＝ _LoadLibrary (取指针_字节集型 (#Dll_ExduiDll), 0, 0)
    全局_初始化 (集_句柄_DLL, 真, #资源_主题包, , { }, “微软雅黑”, 14, #字体风格_普通, #文本渲染模式_ClearType文本和hinting技术, , , )
    集_句柄_窗口 ＝ 窗口_创建 (, , , , 95, 85, 假, , , , 假)
    集_窗口_启动窗口.挂接窗口 (, 集_句柄_窗口, #资源_主题包, , , 取颜色值 (53, 53, 53), #标题栏_隐藏, , #底图方式_拉伸, , 假, { }, , 假)
    集_窗口_启动窗口.总在最前 (真)
    集_句柄_图标 ＝ LoadImageFromBin (#图片_托盘图标, #IMAGE_ICON)
    集_窗口_启动窗口.置托盘图标 (集_句柄_图标, “GoTool”)
    集_窗口_启动窗口.事件_托盘事件 (&事件_托盘事件)
    集_窗口_启动窗口.事件_挂接消息 (&事件_窗口事件)
    集_窗口_启动窗口.置客户区偏移 (0, 0, 0, 0, 真)
    ' ******************************** ********************************
    ' 初始化窗口外观属性
    窗口_置圆角 (集_句柄_窗口, #窗口样式_小半径圆角)
    窗口_可视 (集_句柄_窗口, 假)
    ' ******************************** ********************************
    ' 初始化组件
    集_图形按钮 [1].创建 (集_句柄_窗口, 5, 5, 85, 34, “主界面”, RGB2ARGB (#白色), “微软雅黑”, 14)
    集_图形按钮 [1].点燃图片 (#图片_热点)
    集_图形按钮 [1].按下图片 (#图片_按下)
    集_图形按钮 [1].事件_鼠标左键被放开 (&事件_回调事件)
    ' ********************************
    集_图形按钮 [2].创建 (集_句柄_窗口, 5, 45, 85, 34, “退出”, RGB2ARGB (#白色), “微软雅黑”, 14)
    集_图形按钮 [2].点燃图片 (#图片_热点)
    集_图形按钮 [2].按下图片 (#图片_按下)
    集_图形按钮 [2].事件_鼠标左键被放开 (&事件_回调事件)
    ' ******************************** ********************************
    集_窗口_启动窗口.置进入特效 (#特效类型_无)
    集_窗口_启动窗口.置退出特效 (#特效类型_无)
    窗口_消息循环 (集_句柄_窗口)
    _FreeLibrary (集_句柄_DLL)
    返回 (0) ' 可以根据您的需要返回任意数值

.子程序 事件_窗口事件, 整数型
    .参数 hObj, 整数型
    .参数 Msg, 整数型

    ' 调试输出 (Msg)  ' 测试代码
    .判断开始 (Msg ＝ 642)
        窗口_可视 (集_句柄_窗口, 假)
        返回 (1)
    .默认
        
    .判断结束
    返回 (0)

.子程序 事件_托盘事件
    .参数 hObj, 整数型
    .参数 Msg, 整数型

    .判断开始 (Msg ＝ #事件_鼠标右键被放开)
        ' 调试输出 (“鼠标右键被放开”)  ' 测试代码
        集_窗口_启动窗口.移动 (取鼠标水平位置 (), 取鼠标垂直位置 () － 88)
        集_窗口_启动窗口.调整层次 (#顶层)
        窗口_可视 (集_句柄_窗口, 真)
    .默认
        
    .判断结束
    

.子程序 事件_回调事件
    .参数 hObj, 整数型

    .判断开始 (hObj ＝ 集_图形按钮 [1].取组件句柄 ())
        .如果真 (进程_取同名ID (“GoTool.exe”) ＝ 0)
            运行 (取运行目录 () ＋ “\GoTool.exe”, 假, )
        .如果真结束
        
    .默认
        
    .判断结束
    .判断开始 (hObj ＝ 集_图形按钮 [2].取组件句柄 ())
        集_窗口_启动窗口.删除托盘图标 ()
        DeleteObject (集_句柄_图标)
        进程_结束 (进程_名取ID (“GoTool.exe”))
        结束 ()
    .默认
        
    .判断结束
    

